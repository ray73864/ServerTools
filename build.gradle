buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:0.7.0-SNAPSHOT'
    }
}

String mcversion = "1.7.2"
String forgeversion = "10.12.0.1033"
version = '2.0.0'

apply plugin: 'forge'
apply plugin: 'grgit'

//reobf.setUseRetroGuard(true)

group = "com.matthewprenger.servertools.core"
archivesBaseName = "ServerTools-CORE"

minecraft {
    version = mcversion + "-" + forgeversion
    assetDir = "run/assets"
}

def actualVersion = version

if (System.getenv("BUILD_NUMBER") != null) {
    version = "${minecraft.version}-$version." + System.getenv("BUILD_NUMBER")
    actualVersion += "." + System.getenv("BUILD_NUMBER")
} else if (new File(projectDir, '.git').exists()) {
    def repo = grgit.repo('.')
    version = "${minecraft.version}-$version-${repo.history.log().find().abbreviatedId}"
    actualVersion += "-${repo.history.log().find().abbreviatedId}"
} else {
    version = "${minecraft.version}-$version-DEV"
    actualVersion += "-DEV"
}

subprojects {
    apply plugin: 'java'

    group = "com.matthewprenger.servertools." + name.toLowerCase()
    archivesBaseName = "ServerTools-" + name.toUpperCase()
    version = rootProject.version

    dependencies {
        compile rootProject
    }

    rootProject.reobf {
        reobf(jar) { spec ->
            spec.classpath = sourceSets.main.compileClasspath
        }
    }

    jar {
        destinationDir = new File(rootProject.getBuildDir(), 'libs')
    }
}

allprojects {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'

    processResources {
        // replace stuff in the files we want.
        from(sourceSets.main.resources.srcDirs) {
            include '**/*.info'
            include '**/*.properties'

            // replaces
            expand ([
                'version': actualVersion,
                'mcversion': mcversion
            ])
        }

        // copy everything else, thats we didnt do before
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
            exclude 'version.properties'
        }
    }
}
