buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
    }
}

String actualVersion = "2.0.0"
String mcversion = "1.7.2"
String forgeversion = "10.12.0.1027"

boolean beta = true

version = mcversion + "-" + actualVersion + "." + System.getenv("BUILD_NUMBER");
actualVersion += "." + System.getenv("BUILD_NUMBER");

if (beta) {
    version += "-beta"
    actualVersion += "-beta"
}

apply plugin: 'forge'
group = "com.matthewprenger.servertools.core"
archivesBaseName = "ServerTools-CORE"

minecraft {
    version = mcversion + "-" + forgeversion
    assetDir = "run/assets"
}

subprojects {
    apply plugin: 'java'

    group = "com.matthewprenger.servertools." + name.toLowerCase()
    archivesBaseName = "ServerTools-" + name.toUpperCase()
    version = rootProject.version

    dependencies {
        compile rootProject
    }

    rootProject.reobf {
        reobf(jar) { spec ->
            spec.classpath = sourceSets.main.compileClasspath
        }
    }

    jar {
        destinationDir = new File(rootProject.getBuildDir(), 'libs')
    }
}

allprojects {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'

    jar {
        manifest {
            attributes 'Specification-Version': actualVersion
        }
    }
}
